# Maintainer: Ran Bi <unrevre@gmail.com>

_pkgname=bitmap-fonts
pkgname=${_pkgname}-git
pkgver=0.r7.8aae488
pkgrel=1
pkgdesc="bitmap fonts collection"
arch=('any')
url="https://github.com/unrevre/fonts"
license=('MIT')
depends=()
makedepends=('git' 'fontconfig' 'xorg-bdftopcf')
source=("${_pkgname}::git+https://github.com/unrevre/fonts")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${_pkgname}"
    echo 0.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd "$srcdir/${_pkgname}"

    for f in */*.bdf; do
        bdftopcf "$f" | gzip -9 > "${f%.bdf}.pcf.gz"
    done
}

package() {
    cd "$srcdir/${_pkgname}"

    install -m755 -d "${pkgdir}/usr/share/fonts/misc/"
    install -m644 -t "${pkgdir}/usr/share/fonts/misc/" */*.pcf
    install -m644 -t "${pkgdir}/usr/share/fonts/misc/" */*.pcf.gz

    install -m755 -d "${pkgdir}/usr/share/fonts/TTF/"
    install -m644 -t "${pkgdir}/usr/share/fonts/TTF/" */*.ttf

    install -m755 -d "${pkgdir}/usr/share/kbd/consolefonts/"
    install -m644 -t "${pkgdir}/usr/share/kbd/consolefonts/" */*.psf

    install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/README"
}
